plugins {
    id "com.jfrog.bintray" version "1.3.1"
}

apply plugin: 'java'
apply plugin: 'maven-publish'

def projectName = "bubo"
def versionString = "1.0"
def projectRepo = "FiftyThreeCenter"
def projectDesc = "A small library implementing best practices when using the Observer Pattern with plain ol' Java objects."
def gitUrl = "https://github.com/FiftyThree/Bubo.git"
def projectUrl = "https://github.com/FiftyThree/Bubo"

sourceCompatibility = 1.6
version = "${versionString}"
jar {
    manifest {
        attributes 'Implementation-Title': "${projectName}",
                   'Implementation-Version': "${versionString}"
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

dependencies {
    testCompile 'junit:junit:4.12'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId "com.fiftythree"
            artifactId "${projectName}"
            version "${versionString}"

            from components.java

            artifact sourceJar {
                classifier "sources"
            }

            artifact javadocJar{
                classifier = "javadoc"
            }

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name "${projectName}"
                    description "${projectDesc}"
                    url "${projectUrl}"
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution "${gitUrl}"
                        }
                    }
                }
            }
        }
    }
}

// see https://github.com/bintray/gradle-bintray-plugin for documentation on this plugin.
bintray {
    user = System.getenv('BINTRAY_API_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['mavenJava']
    dryRun = false //Whether to run this as dry-run, without deploying
    publish = true //If version should be auto published after an upload
    pkg {
        repo = "${projectRepo}"
        name = "${projectName}"
        desc = "${projectDesc}"
        websiteUrl = "${projectUrl}"
        issueTrackerUrl = "${projectUrl}/issues"
        vcsUrl = "${gitUrl}"
        licenses = ['Apache-2.0']
        labels = ['java', 'util']
        publicDownloadNumbers = true
        version {
            name = "${versionString}"
            desc = "${projectDesc}"
            vcsTag = "${versionString}"
            released = new Date()
        }
    }
}
